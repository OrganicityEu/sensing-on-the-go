apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 14
        versionName "1.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}

publishing {
    publications {
        OrganiCitySDK(MavenPublication) {
            groupId 'eu.organicity.set.app.sdk'
            artifactId 'sdk'
            version '1.0-SNAPSHOT'
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/sdk-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')

                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
    repositories {
        maven {
            url "https://maven.organicity.eu/content/repositories/snapshots"
            credentials {
                username (project.findProperty("OC_MAVEN_USERNAME"))
                password (project.findProperty("OC_MAVEN_PASSWORD"))
            }
        }
    }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.2'
}
